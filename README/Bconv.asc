%%HP: T(3)A(R)F(.);
\<< -105 DUP
    IF FS?
    THEN CF
    ELSE DROP
    END 

DUP2 DUP 2. <
SWAP 36. > OR SWAP
DUP 2. < SWAP 36. > OR OR 
{ 3 DROPN "Error: 
Invalid Base 
2 \<= base \<= 36" DOERR } IFT 
UNROT IP XQ OVER DUP 

IF TYPE 2. ==  
THEN DUP HEAD NUM 45. \<= { TAIL } IFT 
ELSE ABS DUP
     IFERR XPON \-> xpn 
	   \<<
	      IF xpn ABS 11. > 
	      THEN xpn 0. >  	
           	   { XQ } { 11 RND } IFTE
      	      END 	
           \>> 	
     THEN DROP XQ 
     END \->STR 
END     

DUPDUP "." POS SWAP SIZE
DUP2 == { 1. - UNROT 1. - 1. SWAP SUB 
0. ROT } IFT 
 
\-> bfrm val cpos siz
 
  \<<  	 
      IF bfrm 10. \=/
      THEN
        IF cpos 0. >
        THEN val 1.
	     cpos 1. - SUB val
	     cpos 1. + siz SUB
	     SWAP 'val' STO val
	     SIZE 'siz' STO
        END
        IF val "" \=/
        THEN 0 siz
	     DUP 1. - XQ 0
	     FOR i val
		 OVER i - DUP SUB DUP
		 NUM 65. < { OBJ\-> } {
	         NUM 55. - XQ } IFTE
		 DUP bfrm \>= { 5 DROPN
		 515 DOERR } IFT 
		 bfrm i ^ * ROT + SWAP -1
             STEP DROP
        ELSE val
        END
        IF cpos 0. >
        THEN SWAP
	     'val' STO 0. 1. val SIZE
	     FOR i val i
	         DUP SUB DUP NUM 65. <
        	 { OBJ\-> } { NUM 55. -
	     	 XQ } IFTE DUP bfrm \>=
            	 { 5 DROPN 515 DOERR }
	    	 IFT bfrm i ^ / +
             NEXT \->STR
	     DUP "E" POS 0. > {
	     OBJ\-> 11 RND \->STR } IFT 
	     DUP HEAD NUM 46. \=/ 
	     { TAIL } IFT +
         END
      ELSE val cpos 1. < { OBJ\-> DUP } 
	   { DUP OBJ\-> } IFTE 
	   TYPE 6. == 
	   { 4 DROPN 515 DOERR } IFT
      END

      DUP PICK3 \->STR HEAD NUM 45. ==
      { "-" DUP 6. ROLLD SWAP + } 
      { "" 5. ROLLD } IFTE DUPDUP
      TYPE 2. == SWAP SIZE
      14. < cpos 1. < OR AND 
      { OBJ\-> } IFT 
      "b10" \->TAG ROT "b"
      bfrm + \->TAG UNROT SWAP

      IF DUP TYPE 28. ==
      THEN 0. bfrm ROT
      ELSE \->STR DUPDUP
	   "." POS DUP UNROT 1.
	   - 1. SWAP SUB DUP "" == 
	   { 0 + } IFT OBJ\->
	   UNROT OVER SIZE SUB
	   OBJ\-> bfrm ROT
      END 6. ROLL IP XQ

      \-> val2 bto
      
      \<<
	  IF bto \=/ bto 10. \=/ AND
          THEN { }
    	       DO val2 bto
		  DUP2 MOD DUP 9. > {
		  55. + CHR } IFT UNROT
		  IQUOT UNROT + SWAP
		  'val2' STO
               UNTIL val2 0. ==
               END REVLIST
	       OBJ\-> 2. SWAP
        
	       IF DUP2 \<=
               THEN
         	    FOR i \->STR +
                    NEXT
      	       ELSE DROP2
               END "" ROT
        
               IF DUP 0. \=/
               THEN ROT "."
	            + UNROT 1. OVER \->STR
	            SIZE 1. -
                    FOR i bto *
		        DUP IP DUP 9. > 
		   	{ 55. + CHR } { XQ } IFTE
	                SWAP FP UNROT + SWAP
                    NEXT
               END 
	       DROP + 4. ROLL SWAP +
        
	       IF bto 10. <
               THEN DUPDUP
	            SIZE 14. < SWAP OBJ\->
	            TYPE 28. == OR { OBJ\-> } IFT
               END "b" bto + \->TAG
          ELSE DROP ROT DROP
          END
      \>>
   \>>
\>>